" denite.nvim
" -----------
scriptencoding=utf-8

" INTERFACE
call denite#custom#option('_', {
  \ 'prompt': '>>',
  \ 'empty': 1,
  \ 'winheight': 16,
  \ 'short_source_names': 1,
  \ 'vertical_preview': 1,
  \ })

call denite#custom#option('list', {})

" MATCHERS
" Default is 'matcher_fuzzy'
if has('nvim') && &runtimepath =~# '\/cpsm'
  call denite#custom#source(
    \ 'buffer,file_mru,file_old,file_rec,grep,line',
    \ 'matchers', ['matcher_cpsm', 'matcher_fuzzy'])
endif

" SORTERS
" Default is 'sorter_rank'
call denite#custom#source('z', 'sorters', ['sorter_z'])

" CONVERTERS
" Default is none
call denite#custom#source(
  \ 'buffer,file_mru,file_old',
  \ 'converters', ['converter_relative_word'])

" FIND and GREP COMMANDS
call denite#custom#source('grep', 'args', ['', '', '!']) " force interactive grep
if executable('ag')
  " The Silver Searcher
  call denite#custom#var('file_rec', 'command',
    \ ['ag', '-U', '--hidden', '--follow', '--nocolor', '--nogroup', '-g', ''])

  " Setup ignore patterns in your .agignore file!
  " https://github.com/ggreer/the_silver_searcher/wiki/Advanced-Usage

  call denite#custom#var('grep', 'command', ['ag'])
  call denite#custom#var('grep', 'recursive_opts', [])
  call denite#custom#var('grep', 'pattern_opt', [])
  call denite#custom#var('grep', 'separator', ['--'])
  call denite#custom#var('grep', 'final_opts', [])
  call denite#custom#var('grep', 'default_opts',
    \ [ '--vimgrep', '--smart-case', '--hidden' ])

elseif executable('ack')
  " Ack command
  call denite#custom#var('grep', 'command', ['ack'])
  call denite#custom#var('grep', 'recursive_opts', [])
  call denite#custom#var('grep', 'pattern_opt', ['--match'])
  call denite#custom#var('grep', 'separator', ['--'])
  call denite#custom#var('grep', 'final_opts', [])
  call denite#custom#var('grep', 'default_opts',
    \ ['--ackrc', $HOME.'/.config/ackrc', '-H',
    \ '--nopager', '--nocolor', '--nogroup', '--column'])
endif

" KEY MAPPINGS
let s:insert_mode_mappings = [
  \  ['jj', '<denite:enter_mode:normal>', 'noremap'],
  \  ['<Esc>', '<denite:enter_mode:normal>', 'noremap'],
  \  ['<C-N>', '<denite:assign_next_matched_text>', 'noremap'],
  \  ['<C-P>', '<denite:assign_previous_matched_text>', 'noremap'],
  \  ['<Up>', '<denite:assign_previous_text>', 'noremap'],
  \  ['<Down>', '<denite:assign_next_text>', 'noremap'],
  \  ['<C-Y>', '<denite:redraw>', 'noremap'],
  \  ['<C-X>', '<denite:quick_move>', 'noremap'],
  \ ]

let s:normal_mode_mappings = [
  \   ["'", '<denite:toggle_select_down>', 'noremap'],
  \   ['<C-n>', '<denite:jump_to_next_source>', 'noremap'],
  \   ['<C-p>', '<denite:jump_to_previous_source>', 'noremap'],
  \   ['gg', '<denite:move_to_first_line>', 'noremap'],
  \   ['st', '<denite:do_action:tabopen>', 'noremap'],
  \   ['sg', '<denite:do_action:vsplit>', 'noremap'],
  \   ['sv', '<denite:do_action:split>', 'noremap'],
  \   ['sc', '<denite:quit>', 'noremap'],
  \   ['r', '<denite:redraw>', 'noremap'],
  \ ]

for s:m in s:insert_mode_mappings
  call denite#custom#map('insert', s:m[0], s:m[1], s:m[2])
endfor
for s:m in s:normal_mode_mappings
  call denite#custom#map('normal', s:m[0], s:m[1], s:m[2])
endfor

" vim: set ts=2 sw=2 tw=80 expandtab :
